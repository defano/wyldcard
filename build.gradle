apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'antlr'
apply plugin: 'idea'


allprojects {
	apply plugin: 'java'
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
}

repositories {
	mavenCentral()
	mavenLocal()
}

ext {
	jdk = System.env.'JAVA_HOME'
}

configurations {
	antlr
}

clean {
    delete "generated-src/"
	delete "bundles/"
}

sourceSets {
	main.java.srcDirs += 'generated-src/main/java'
}

idea {
	module {
		sourceDirs += file('generated-src/main/java')
	}
}

dependencies {
	antlr "org.antlr:antlr4:4.7.1"

	compile 'com.intellij:javac2:7.0.3'
	compile 'com.google.code.gson:gson:2.8.1'
	compile 'com.google.guava:guava:19.0'
	compile ('com.defano.jmonet:jmonet:0.2.2') {changing = true}
	compile ('com.defano.jsegue:jsegue:0.0.1') {changing = true}
	compile 'org.bitbucket.cowwoc.diff-match-patch:diff-match-patch:1.0'
	compile 'com.l2fprod.common:l2fprod-common-fontchooser:6.9.1'

	testCompile 'junit:junit:4.12'
	testCompile group: 'commons-io', name: 'commons-io', version: '2.4'

}

generateGrammarSource {
	outputDirectory = file("generated-src/main/java/")
	maxHeapSize = "64m"
	arguments += ["-long-messages", "-no-listener", "-visitor", "-package", "com.defano.hypertalk.parser"]
}

jar {
	exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.MF'

	from {
		(configurations.runtime).collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}

	manifest {
		attributes 'Main-Class': 'com.defano.hypercard.HyperCard'
	}
}

task generateBundle(type: Exec) {
	dependsOn build
	workingDir "${project.projectDir.absolutePath}"
	commandLine "${jdk}/bin/javapackager",
			"-deploy",
			"-native",
			"-outdir", ".",
			"-outfile", "HyperTalk-Java.app",
			"-srcfiles", "./build/libs/hypertalk-java.jar",
            "-appclass", "com.defano.hypercard.HyperCard",
			"-name", "HyperTalk Java",
			"-title", "HyperTalk Java"
}

mainClassName="com.defano.hypercard.HyperCard"
